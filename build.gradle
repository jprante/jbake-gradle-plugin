
plugins {
    id "com.gradle.plugin-publish" version "0.9.6"
}

group = 'org.xbib.gradle.plugin'
version = '1.2.0'

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: "com.gradle.plugin-publish"

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    wagon
}

dependencies {
    compile gradleApi()
    compileOnly 'org.jbake:jbake-core:2.5.0'
    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude group:'org.codehaus.groovy'
    }
    wagon 'org.apache.maven.wagon:wagon-ssh-external:2.10'
}

task javadocJar(type: Jar, dependsOn: classes) {
    from javadoc
    classifier 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier 'sources'
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}

apply from: 'gradle/ext.gradle'
apply from: 'gradle/publish.gradle'


if (project.hasProperty('gradle.publish.key')) {
    pluginBundle {
        website = scmUrl
        vcsUrl = scmUrl
        plugins {
            jbakePlugin {
                id = 'org.xbib.gradle.plugin.jbake'
                version = project.version
                description = projectDescription
                displayName = 'Gradle JBake plugin'
                tags = ['jbake']
            }
        }
    }
}